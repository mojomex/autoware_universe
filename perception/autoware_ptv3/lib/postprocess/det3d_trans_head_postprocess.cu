// Copyright 2026 TIER IV, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

#include "autoware/ptv3/postprocess/det3d_trans_head_postprocess.hpp"

#include <autoware/cuda_utils/cuda_check_error.hpp>
#include <autoware/cuda_utils/cuda_unique_ptr.hpp>

#include <cuda_fp16.h>
#include <cuda_runtime.h>
#include <thrust/copy.h>
#include <thrust/count.h>
#include <thrust/device_ptr.h>
#include <thrust/execution_policy.h>
#include <thrust/sort.h>

#include <vector>

namespace autoware::ptv3
{

namespace
{
constexpr int k_block_size = 256;

struct IsScoreKeep
{
  __device__ bool operator()(const Box3D & box) const { return box.score > 0.0f; }
};

struct ScoreGreater
{
  __device__ bool operator()(const Box3D & lhs, const Box3D & rhs) const
  {
    return lhs.score > rhs.score;
  }
};
}  // namespace

template <typename FloatType, typename LabelType>
__global__ void decode_trans_head_to_boxes3d_kernel(
  const FloatType * __restrict__ query_heatmap_score, const LabelType * __restrict__ query_labels,
  const FloatType * __restrict__ heatmap, const FloatType * __restrict__ center,
  const FloatType * __restrict__ height, const FloatType * __restrict__ dim,
  const FloatType * __restrict__ rot, const FloatType * __restrict__ vel, int num_proposals,
  int num_classes, float bbox_downsample_factor, float bbox_voxel_x_size, float bbox_voxel_y_size,
  float min_x_range, float min_y_range, const float * __restrict__ post_center_range,
  const float * __restrict__ score_thresholds, const float * __restrict__ dist_bin_limits,
  int num_dist_bins, const float * __restrict__ yaw_norm_thresholds, bool has_twist,
  Box3D * __restrict__ out_boxes)
{
  const int i = blockIdx.x * blockDim.x + threadIdx.x;
  if (i >= num_proposals) {
    return;
  }

  out_boxes[i] = Box3D{};

  const int label = static_cast<int>(query_labels[i]);
  if (label < 0 || label >= num_classes) {
    return;
  }

  const int class_offset = label * num_proposals;
  const float score = (1.0f / (1.0f + expf(-static_cast<float>(heatmap[class_offset + i])))) *
                      static_cast<float>(query_heatmap_score[class_offset + i]);

  const float x =
    static_cast<float>(center[i]) * bbox_downsample_factor * bbox_voxel_x_size + min_x_range;
  const float y =
    static_cast<float>(center[num_proposals + i]) * bbox_downsample_factor * bbox_voxel_y_size +
    min_y_range;
  const float length = expf(static_cast<float>(dim[i]));
  const float width = expf(static_cast<float>(dim[num_proposals + i]));
  const float box_h = expf(static_cast<float>(dim[2 * num_proposals + i]));
  const float z = static_cast<float>(height[i]) - box_h * 0.5f;

  if (
    x < post_center_range[0] || y < post_center_range[1] || z < post_center_range[2] ||
    x > post_center_range[3] || y > post_center_range[4] || z > post_center_range[5]) {
    return;
  }

  const float radial = sqrtf(x * x + y * y);
  float threshold = 1.0e9f;
  for (int d = 0; d < num_dist_bins; ++d) {
    if (radial < dist_bin_limits[d]) {
      threshold = score_thresholds[d * num_classes + label];
      break;
    }
  }
  if (score < threshold) {
    return;
  }

  const float yaw_sin = static_cast<float>(rot[i]);
  const float yaw_cos = static_cast<float>(rot[num_proposals + i]);
  const float yaw_norm = sqrtf(yaw_sin * yaw_sin + yaw_cos * yaw_cos);
  if (yaw_norm < yaw_norm_thresholds[label]) {
    return;
  }

  out_boxes[i].label = label;
  out_boxes[i].score = score;
  out_boxes[i].x = x;
  out_boxes[i].y = y;
  out_boxes[i].z = z;
  out_boxes[i].length = length;
  out_boxes[i].width = width;
  out_boxes[i].height = box_h;
  out_boxes[i].yaw = atan2f(yaw_sin, yaw_cos);
  out_boxes[i].vel_x = has_twist ? static_cast<float>(vel[i]) : 0.0F;
  out_boxes[i].vel_y = has_twist ? static_cast<float>(vel[num_proposals + i]) : 0.0F;
}

template <typename FloatType, typename IntType>
void launch_decode_trans_head_to_boxes3d(
  const FloatType * query_heatmap_score_d, const IntType * query_labels_d,
  const FloatType * heatmap_d, const FloatType * center_d, const FloatType * height_d,
  const FloatType * dim_d, const FloatType * rot_d, const FloatType * vel_d, int num_proposals,
  int num_classes, float bbox_downsample_factor, float bbox_voxel_x_size, float bbox_voxel_y_size,
  float min_x_range, float min_y_range, const float * post_center_range_d,
  const float * score_thresholds_d, const float * dist_bin_limits_d, int num_dist_bins,
  const float * yaw_norm_thresholds_d, bool has_twist, Box3D * out_boxes_d, cudaStream_t stream)
{
  const int grid = (num_proposals + k_block_size - 1) / k_block_size;
  decode_trans_head_to_boxes3d_kernel<FloatType, IntType><<<grid, k_block_size, 0, stream>>>(
    query_heatmap_score_d, query_labels_d, heatmap_d, center_d, height_d, dim_d, rot_d, vel_d,
    num_proposals, num_classes, bbox_downsample_factor, bbox_voxel_x_size, bbox_voxel_y_size,
    min_x_range, min_y_range, post_center_range_d, score_thresholds_d, dist_bin_limits_d,
    num_dist_bins, yaw_norm_thresholds_d, has_twist, out_boxes_d);
}

// Explicit instantiations for launch_decode_trans_head_to_boxes3d
template void launch_decode_trans_head_to_boxes3d<float, std::int32_t>(
  const float *, const std::int32_t *, const float *, const float *, const float *, const float *,
  const float *, const float *, int, int, float, float, float, float, float, const float *,
  const float *, const float *, int, const float *, bool, Box3D *, cudaStream_t);
template void launch_decode_trans_head_to_boxes3d<float, std::int64_t>(
  const float *, const std::int64_t *, const float *, const float *, const float *, const float *,
  const float *, const float *, int, int, float, float, float, float, float, const float *,
  const float *, const float *, int, const float *, bool, Box3D *, cudaStream_t);
template void launch_decode_trans_head_to_boxes3d<__half, std::int32_t>(
  const __half *, const std::int32_t *, const __half *, const __half *, const __half *,
  const __half *, const __half *, const __half *, int, int, float, float, float, float, float,
  const float *, const float *, const float *, int, const float *, bool, Box3D *, cudaStream_t);
template void launch_decode_trans_head_to_boxes3d<__half, std::int64_t>(
  const __half *, const std::int64_t *, const __half *, const __half *, const __half *,
  const __half *, const __half *, const __half *, int, int, float, float, float, float, float,
  const float *, const float *, const float *, int, const float *, bool, Box3D *, cudaStream_t);

Det3dTransHeadPostprocess::Det3dTransHeadPostprocess(const PTv3Config & config, cudaStream_t stream)
: config_(config)
{
  raw_boxes_d_ = autoware::cuda_utils::make_unique<Box3D[]>(config_.num_proposals_);
  passing_boxes_d_ = autoware::cuda_utils::make_unique<Box3D[]>(config_.num_proposals_);
  yaw_norm_thresholds_d_ =
    autoware::cuda_utils::make_unique<float[]>(config_.yaw_norm_thresholds_.size());
  CHECK_CUDA_ERROR(cudaMemcpyAsync(
    yaw_norm_thresholds_d_.get(), config_.yaw_norm_thresholds_.data(),
    config_.yaw_norm_thresholds_.size() * sizeof(float), cudaMemcpyHostToDevice, stream));

  post_center_range_d_ = autoware::cuda_utils::make_unique<float[]>(6);
  CHECK_CUDA_ERROR(cudaMemcpyAsync(
    post_center_range_d_.get(), config_.post_center_range_.data(), 6 * sizeof(float),
    cudaMemcpyHostToDevice, stream));

  score_thresholds_d_ =
    autoware::cuda_utils::make_unique<float[]>(config_.detection_score_thresholds_.size());
  CHECK_CUDA_ERROR(cudaMemcpyAsync(
    score_thresholds_d_.get(), config_.detection_score_thresholds_.data(),
    config_.detection_score_thresholds_.size() * sizeof(float), cudaMemcpyHostToDevice, stream));

  dist_bin_limits_d_ =
    autoware::cuda_utils::make_unique<float[]>(config_.distance_bin_upper_limits_.size());
  CHECK_CUDA_ERROR(cudaMemcpyAsync(
    dist_bin_limits_d_.get(), config_.distance_bin_upper_limits_.data(),
    config_.distance_bin_upper_limits_.size() * sizeof(float), cudaMemcpyHostToDevice, stream));
}

template <typename FloatType, typename IntType>
cudaError_t Det3dTransHeadPostprocess::process(
  const FloatType * query_heatmap_score_d, const IntType * query_labels_d,
  const FloatType * heatmap_d, const FloatType * center_d, const FloatType * height_d,
  const FloatType * dim_d, const FloatType * rot_d, const FloatType * vel_d, cudaStream_t stream)
{
  num_boxes_ = 0;

  launch_decode_trans_head_to_boxes3d<FloatType, IntType>(
    query_heatmap_score_d, query_labels_d, heatmap_d, center_d, height_d, dim_d, rot_d, vel_d,
    static_cast<int>(config_.num_proposals_),
    static_cast<int>(config_.detection_class_names_.size()),
    static_cast<float>(config_.bbox_downsample_factor_), config_.bbox_voxel_x_size_,
    config_.bbox_voxel_y_size_, config_.min_x_range_, config_.min_y_range_,
    post_center_range_d_.get(), score_thresholds_d_.get(), dist_bin_limits_d_.get(),
    static_cast<int>(config_.distance_bin_upper_limits_.size()), yaw_norm_thresholds_d_.get(),
    config_.has_twist_, raw_boxes_d_.get(), stream);

  const auto policy = thrust::cuda::par.on(stream);
  auto raw_begin = thrust::device_pointer_cast(raw_boxes_d_.get());
  auto raw_end = raw_begin + config_.num_proposals_;
  auto passing_begin = thrust::device_pointer_cast(passing_boxes_d_.get());

  const auto num_passing = thrust::count_if(policy, raw_begin, raw_end, IsScoreKeep{});
  if (num_passing == 0) {
    return cudaGetLastError();
  }

  const auto passing_end = thrust::copy_if(
    policy, raw_begin, raw_end, passing_begin, IsScoreKeep{});
  thrust::sort(policy, passing_begin, passing_end, ScoreGreater{});

  num_boxes_ = static_cast<std::size_t>(num_passing);
  return cudaGetLastError();
}

// Explicit instantiations for process
template cudaError_t Det3dTransHeadPostprocess::process<float, std::int32_t>(
  const float *, const std::int32_t *, const float *, const float *, const float *, const float *,
  const float *, const float *, cudaStream_t);
template cudaError_t Det3dTransHeadPostprocess::process<float, std::int64_t>(
  const float *, const std::int64_t *, const float *, const float *, const float *, const float *,
  const float *, const float *, cudaStream_t);
template cudaError_t Det3dTransHeadPostprocess::process<__half, std::int32_t>(
  const __half *, const std::int32_t *, const __half *, const __half *, const __half *,
  const __half *, const __half *, const __half *, cudaStream_t);
template cudaError_t Det3dTransHeadPostprocess::process<__half, std::int64_t>(
  const __half *, const std::int64_t *, const __half *, const __half *, const __half *,
  const __half *, const __half *, const __half *, cudaStream_t);

}  // namespace autoware::ptv3
